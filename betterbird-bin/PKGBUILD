# Maintainer: Piotr Gorski <lucjan.lucjanov@gmail.com>
# Contrubutor: Posi<posi1981@gmail.com>

pkgname=betterbird-bin
_pkgname=betterbird
_pkgver=102.15.0
_build=bb40
pkgver=$_pkgver.$_build
pkgrel=1.1
pkgdesc="Betterbird is a fine-tuned version of Mozilla Thunderbird, Thunderbird on steroids, if you will."
arch=('x86_64')
url="https://www.betterbird.eu/index.html"
license=('MPL2')
depends=('dbus-glib')
provides=('betterbird')
conflicts=()
source=(
    "https://www.betterbird.eu/downloads/LinuxArchive/${_pkgname}-${_pkgver//_/-}-${_build}.en-US.linux-x86_64.tar.bz2"
    "betterbird.desktop"
)

package() {
    install -d "${pkgdir}/opt"
    install -d "${pkgdir}/usr/bin"
    install -d "${pkgdir}/usr/share/applications"

    cp -r "${srcdir}/${_pkgname}/" "${pkgdir}/opt/${_pkgname}"
    install -m644 "${srcdir}/${_pkgname}.desktop" "${pkgdir}/usr/share/applications/${_pkgname}.desktop"
    ln -s /opt/$_pkgname/betterbird "$pkgdir"/usr/bin/$_pkgname

    #icons
    for i in 16 22 24 32 48 64 128 256; do
        install -d "$pkgdir"/usr/share/icons/hicolor/${i}x${i}/apps/
        ln -s /opt/$_pkgname/chrome/icons/default/default$i.png \
            "$pkgdir"/usr/share/icons/hicolor/${i}x${i}/apps/$_pkgname.png
    done
}
sha256sums=('85556b000db417b2009d41a328f5b584c8fd70b0a714ca8ad5c6a3d5d5dd06f1'
            '816a97383c4eba202d9993736d14d3f728064d1bea7301adc93ed1248a096ca8')
