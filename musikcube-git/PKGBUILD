# Maintainer: Damian N. <nycko123 at gmail>
# Maintainer: Hans-Nikolai Viessmann <hans AT viess DOT mn

pkgname=musikcube-git
pkgver=0.97.0.r26.ge4593ba0
pkgrel=1
pkgdesc="A terminal-based cross-platform music player, audio engine, metadata indexer, and server. (no httpserver, alsa output) (manual install)"
arch=('x86_64')
url="https://github.com/clangen/musikcube"
license=('BSD')
depends=('faad2' 'ffmpeg' 'libev' 'libogg' 'libvorbis' 'flac' 'lame'
         'ncurses' 'boost' 'libpulse' 'alsa-lib' 'curl')
makedepends=('cmake' 'git')
provides=('musikcube')
conflicts=('musikcube')
source=('musikcube::git+https://github.com/clangen/musikcube.git')
md5sums=('SKIP')
options=(!buildflags !debug strip)

pkgver() {
    cd "$srcdir/${pkgname%-git}"
    git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
    cd "$srcdir/${pkgname%-git}"
	## remove few plugins from CMakeLists
	sed -iE 's|libmicrohttpd\${DEB_MICROHTTPD_VERSION}, ||g' CMakeLists.txt
	sed -iE 's| server||;s| httpdatastream||g' CMakeLists.txt
	sed -iE 's|add_subdirectory(src/plugins/server)||g' CMakeLists.txt
	sed -iE 's|add_subdirectory(src/plugins/httpdatastream)||g' CMakeLists.txt
	sed -iE 's|add_plugin("src/plugins/server" "server")||g' CMakeLists.txt
	sed -iE 's|add_plugin("src/plugins/httpdatastream" "httpdatastream")||g' CMakeLists.txt
	sed -iE 's|add_plugin("src/plugins/libopenmptdecoder" "openmptdecoder")||g' CMakeLists.txt
	sed -iE 's|add_plugin("src/plugins/pulseout" "pulseout")||g' CMakeLists.txt
	sed -iE 's|add_plugin("src/plugins/pipewireout" "pipewireout")||g' CMakeLists.txt
	sed -iE 's|add_plugin("src/plugins/sndioout" "sndioout")||g' CMakeLists.txt
	sed -iE 's|add_plugin("src/plugins/coreaudioout" "coreaudioout")||g' CMakeLists.txt
	sed -iE 's|add_plugin("src/plugins/mpris" "mpris")||g' CMakeLists.txt
	sed -iE 's|add_plugin("src/plugins/macosmediakeys" "macosmediakeys")||g' CMakeLists.txt
	## remove few plugins dirs
	\rm -rv src/plugins/{libopenmptdecoder,httpdatastream,macos*,win32*,pulseout,pipewireout,sndioout*,coreaudioout,mpris,server}

}

build() {
    cd "$srcdir/${pkgname%-git}"
    cmake -B build -S "${PWD}" \
        -DCMAKE_BUILD_TYPE=Release \
        -DNO_NCURSESW=true \
        -DENABLE_MPRIS=false \
        -DLIB_SYSTEMD=false \
        -DENABLE_SYSTEMD=false \
        -DENABLE_PULSEAUDIO=false \
        -DENABLE_PIPEWIRE=false \
        -DENABLE_LIBOPENMTP=false \
        -DENABLE_OPENMTP=false \
        -DENABLE_ALSA=true \
        -DCMAKE_SKIP_RPATH=YES \
        -DCMAKE_INSTALL_PREFIX=/usr
    make -C build
}

package() {
    cd "$srcdir/${pkgname%-git}"
    make -C build DESTDIR="$pkgdir/" install

    install -Dm644 "LICENSE.txt" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE.txt"
	# temp symlink lib .so file
	mkdir -p "${pkgdir}/usr/lib"
	cd "${pkgdir}"
	ln -sf "/usr/share/musikcube/libmusikcore.so" "${pkgdir}/usr/lib/libmusikcore.so"
}
