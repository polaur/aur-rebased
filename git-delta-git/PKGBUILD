# Maintainer: Piotr GÃ³rski <lucjan.lucjanov@gmail.com>
# Contributor: George Rawlinson <grawlinson@archlinux.org>

pkgname=git-delta-git
_pkgname=delta
pkgver=0.14.0.r3.g5c570f4
pkgrel=1
pkgdesc="A syntax-highlighting pager for git"
arch=('any')
url="https://github.com/dandavison/delta"
license=('custom')
depends=('libgit2' 'libgit2.so')
makedepends=('rust' 'mdbook' 'git')
checkdepends=('git')
provides=('delta' 'git-delta')
conflicts=('delta' 'git-delta')
source=(git+https://github.com/dandavison/$_pkgname.git)
md5sums=('SKIP')

pkgver() {
  cd $_pkgname
  git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
  cd $_pkgname

  # download dependencies
  cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

build() {
  cd $_pkgname
  # https://archlinux.org/todo/lto-fat-objects/
  CFLAGS+=' -ffat-lto-objects'

  # binary
  cargo build --frozen --release --all-features

  # html manual
  cd manual
  mdbook build --dest-dir html
}

check() {
  cd $_pkgname
  # some tests hang for 60+ seconds, find out why
  cargo test --frozen --all-features
}

package() {
  cd $_pkgname
  # binary
  install -vDm755 -t "$pkgdir/usr/bin" target/release/delta

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE

  # shell auto-completions
  install -vDm644 etc/completion/completion.bash \
    "$pkgdir/usr/share/bash-completion/completions/$_pkgname"
  install -vDm644 etc/completion/completion.zsh \
    "$pkgdir/usr/share/zsh/site-functions/_$_pkgname"
  install -vDm644 etc/completion/completion.fish \
    "$pkgdir/usr/share/fish/vendor_completions.d/$_pkgname.fish"

  # documentation
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" README.md

  # html manual
  cp -r manual/html "$pkgdir/usr/share/doc/$pkgname"
}
