# Maintainer: Piotr Gorski <lucjan.lucjanov@gmail.com>
# Contributor: Jon Nordby <jononor@gmail.com>
# Contributor: Pablo Lezaeta <prflr88@gmail.com_

pkgname=dracut-git
_pkgname=dracut
pkgver=059.99.gd1187543
pkgrel=1
pkgdesc="An event driven initramfs infrastructure"
arch=('x86_64')
url="https://dracut.wiki.kernel.org"
license=('GPL')
depends=('bash' 'coreutils' 'cpio' 'filesystem' 'findutils' 'grep' 'gzip'
         'kmod' 'pkgconf' 'procps-ng' 'sed' 'systemd' 'util-linux' 'xz')
makedepends=('asciidoc' 'bash-completion' 'git')
optdepends=('binutils: --uefi option support'
            'elfutils: strip binaries to reduce initramfs size'
            'hardlink: --hardlink option support'
            'multipath-tools: dmraid dracut module support'
            'pigz: faster gzip compression'
            'sbsigntools: uefi_secureboot_cert/key configuration option support')
provides=('initramfs' 'dracut')
conflicts=('dracut')
backup=('etc/dracut.conf')
source=("git+https://github.com/dracutdevs/dracut"
        "dracut-rebuild"
        "dracut-install"
        "dracut-remove"
        "90-dracut-install.hook"
        "60-dracut-remove.hook"
        "dracut-cachyos.conf"
        "snapshot-overlay.sh"
        "module-setup.sh")
sha512sums=('SKIP'
            '6457ce228e78e1d7f07eafec6f37484264004e53e170b357093d2a92c024588bc28a4c4664539e7ca42a6e6838f156ca7cb047f3b8c927228d6ead2a43786d2f'
            '7cc41b1025e46f14068da395a7b6647ad18df15197c1fe38a0b91e03addebcc8f2dfcb42292e9df1b55c8f4dd2892212a720eab258e80ea098037fe49d8e83c1'
            '57cf5efc3e0a5540cd1d9d617bdc5c122182d2416f9e4cb484a874c32c758fb347477c36f36684660a2a2cc8dd15c3c2a5a56237b3d0ddef3d47ce401786472a'
            '2a5df7088a1db47ec2225c907cfbecd544fff47e031f5c3c435c30e456b603ce0ecada606d5f69e54714448808b5c359fb3c38a69e5c60be3744371e8a2b5b5c'
            'b84677cc05865d1571723658f2661cb749acf9039133b95893c2cfddc30070f885e8cd047aaa5dbc04dd3d8f9f6a4aa6573ac916c70edfd1daab4ecc5c582980'
            'aaad69c74caa94915c05618af70ce10ef75e71ef38b8e4b4ddcc4f3a741debf6005c647c65e733cf415f69dab9a5b3632f1c2dbb16c7b1014ae930ce5cba16c4'
            'cd755060b6a2c534f8ee5ec9d1fff18ee71250db1ab4105fc9df4c28e1634fad031944b94790da8b29f296e013631bd61731423c1f847e95b1bd415a7a5ca4a5'
            '461231a756b6bc250fb973a5b4b6fbc0b2d6f8dae0dbc89a37bab17745dcfe38d9ae6cfe58bd4b9c4661e5f0024577ad9e4aa1d3c16169e7a83744da4b9068a5')

_backports=(
)

_reverts=(
)

pkgver() {   
  cd "$srcdir/${_pkgname}"
  git describe --long --tag | sed 's/^v//;s/-/./g'
}

prepare() {
  cd "$srcdir/${_pkgname}"

  local _c
  
  for _c in "${_backports[@]}"; do
    git log --oneline -1 "${_c}"  
    git cherry-pick -n "${_c}"
  done

  for _c in "${_reverts[@]}"; do
    git log --oneline -1 "${_c}"
    git revert -n "${_c}"
  done
}

build() {
  local prefix=/usr sysconfdir=/etc
  
  cd "$srcdir/${_pkgname}"

  ./configure \
    --sysconfdir=${sysconfdir} \
    --prefix=${prefix} \
    --libdir=${prefix}/lib \
    --systemdsystemunitdir=${prefix}/lib/systemd/system \
    --bashcompletiondir=$(pkg-config --variable=completionsdir bash-completion)
  make
}

package() {
  cd "$srcdir/${_pkgname}"

  DESTDIR="$pkgdir" make install
  install -Dm644 "${srcdir}/90-dracut-install.hook" "${pkgdir}/usr/share/libalpm/hooks/90-dracut-install.hook"
  install -Dm644 "${srcdir}/60-dracut-remove.hook"  "${pkgdir}/usr/share/libalpm/hooks/60-dracut-remove.hook"
  install -Dm755 "${srcdir}/dracut-install"         "${pkgdir}/usr/share/libalpm/scripts/dracut-install"
  install -Dm755 "${srcdir}/dracut-remove"          "${pkgdir}/usr/share/libalpm/scripts/dracut-remove"
  install -Dm755 "${srcdir}/dracut-rebuild"         "${pkgdir}/usr/bin/dracut-rebuild"
  install -Dm644 "${srcdir}/dracut-cachyos.conf"    "${pkgdir}/etc/dracut-cachyos.conf"
  install -Dm755 "${srcdir}/snapshot-overlay.sh"    "${pkgdir}/usr/lib/dracut/modules.d/91btrfs-snapshot-overlay/snapshot-overlay.sh"
  install -Dm755 "${srcdir}/module-setup.sh"        "${pkgdir}/usr/lib/dracut/modules.d/91btrfs-snapshot-overlay/module-setup.sh"
}
