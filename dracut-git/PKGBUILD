# Maintainer: Piotr Gorski <lucjan.lucjanov@gmail.com>
# Contributor: Jon Nordby <jononor@gmail.com>
# Contributor: Pablo Lezaeta <prflr88@gmail.com_

pkgname=dracut-git
_pkgname=dracut
pkgver=060.r6794.g4980bad3
pkgrel=2
pkgdesc="An event driven initramfs infrastructure"
arch=('x86_64')
url="https://dracut.wiki.kernel.org"
license=('GPL-2.0-or-later')
depends=(
  'bash'
  'coreutils'
  'cpio'
  'filesystem'
  'findutils'
  'gawk'
  'grep'
  'kmod'
  'pkgconf'
  'procps-ng'
  'sed'
  'systemd'
  'util-linux'
)
makedepends=(
  'asciidoc'
  'bash-completion'
  'git'
)
optdepends=(
  'binutils: --uefi option support'
  'elfutils: strip binaries to reduce initramfs size'
  'gzip: gzip compression'
  'multipath-tools: dmraid dracut module support'
  'pigz: faster gzip compression'
  'sbsigntools: uefi_secureboot_cert/key configuration option support'
  'tpm2-tools: tpm2 support for e.g. LUKS'
  'xz: xz compression'
)
provides=('initramfs' 'dracut')
conflicts=('dracut')
backup=('etc/dracut.conf' 'etc/dracut-cachyos.conf')
source=("git+https://github.com/dracutdevs/dracut"
        "dracut-rebuild"
        "dracut-install"
        "dracut-remove"
        "90-dracut-install.hook"
        "60-dracut-remove.hook"
        "dracut-cachyos.conf"
        "snapshot-overlay.sh"
        "module-setup.sh"
        'dracut-systemd-255-pcrphase.patch')
sha512sums=('SKIP'
            '6457ce228e78e1d7f07eafec6f37484264004e53e170b357093d2a92c024588bc28a4c4664539e7ca42a6e6838f156ca7cb047f3b8c927228d6ead2a43786d2f'
            'f298693ee1a637329b1a38c746151c2ae8d2ae431f894c3001f55d812a2a20a68907ffa0b04ba748c18a2898cc30e0f49e75d0d13c7dd453129304f3cfd4621e'
            'd687dd7951ffabe696a2f6b5f27d8446173c61f5c744f447065045c702c24c0bd89eb610c6a3ec0f56c5afb8aeff2199a1c2bfd7926f0a5f817e0f53c3c92529'
            '2591bffab70e7e91f4ff776fe740ac28bbf7b8a87b02773a6f408110aa5ad0a56d2436934ddb7ab5d4aad1b82f324480736f45b93baa6efa1c14da7465a1420c'
            'b84677cc05865d1571723658f2661cb749acf9039133b95893c2cfddc30070f885e8cd047aaa5dbc04dd3d8f9f6a4aa6573ac916c70edfd1daab4ecc5c582980'
            '3fb23adbf462d655638a6a5a3083b464150b05b088e9139fceda500ba7560a29eb90d0e4dab896a257264fc6e72ec537a0519ad4e4e25e4c44b2dc3263cb5a52'
            'cd755060b6a2c534f8ee5ec9d1fff18ee71250db1ab4105fc9df4c28e1634fad031944b94790da8b29f296e013631bd61731423c1f847e95b1bd415a7a5ca4a5'
            '461231a756b6bc250fb973a5b4b6fbc0b2d6f8dae0dbc89a37bab17745dcfe38d9ae6cfe58bd4b9c4661e5f0024577ad9e4aa1d3c16169e7a83744da4b9068a5'
            '947587274ac11d4186b28c3894298da615ca1f72e56ba8278fb647a0068f38e84bd68720015caba9881219605296a19633674217b3fe89b5ff699791938a0ab7')

_backports=(
)

_reverts=(
)

pkgver() {   
  cd "$srcdir/${_pkgname}"
  #git describe --long --tag | sed 's/^v//;s/-/./g'
  _ver="$(cat dracut-version.sh | grep -m1 'DRACUT_VERSION' | grep -o "[[:digit:]]*" | paste -sd'.')"
  echo "${_ver}.r$(git rev-list --count HEAD).g$(git rev-parse --short HEAD)"
}

prepare() {
  cd "$srcdir/${_pkgname}"

  local _c

  for _c in "${_backports[@]}"; do
    git log --oneline -1 "${_c}"  
    git cherry-pick -n "${_c}"
  done

  for _c in "${_reverts[@]}"; do
    git log --oneline -1 "${_c}"
    git revert -n "${_c}"
  done

  local src

  for src in "${source[@]}"; do
    src="${src%%::*}"
    src="${src##*/}"
    [[ $src = *.patch ]] || continue
    msg2 "Applying patch $src..."
    patch -Np1 < "../$src"
  done
}

build() {
  local prefix=/usr sysconfdir=/etc

  cd "$srcdir/${_pkgname}"

  ./configure \
    --sysconfdir=${sysconfdir} \
    --prefix=${prefix} \
    --libdir=${prefix}/lib \
    --systemdsystemunitdir=${prefix}/lib/systemd/system \
    --bashcompletiondir=$(pkg-config --variable=completionsdir bash-completion)
  make
}

package() {
  cd "$srcdir/${_pkgname}"

  DESTDIR="$pkgdir" make install
  install -Dm644 "${srcdir}/90-dracut-install.hook" "${pkgdir}/usr/share/libalpm/hooks/90-dracut-install.hook"
  install -Dm644 "${srcdir}/60-dracut-remove.hook"  "${pkgdir}/usr/share/libalpm/hooks/60-dracut-remove.hook"
  install -Dm755 "${srcdir}/dracut-install"         "${pkgdir}/usr/share/libalpm/scripts/dracut-install"
  install -Dm755 "${srcdir}/dracut-remove"          "${pkgdir}/usr/share/libalpm/scripts/dracut-remove"
  install -Dm755 "${srcdir}/dracut-rebuild"         "${pkgdir}/usr/bin/dracut-rebuild"
  install -Dm644 "${srcdir}/dracut-cachyos.conf"    "${pkgdir}/etc/dracut-cachyos.conf"
  install -Dm755 "${srcdir}/snapshot-overlay.sh"    "${pkgdir}/usr/lib/dracut/modules.d/91btrfs-snapshot-overlay/snapshot-overlay.sh"
  install -Dm755 "${srcdir}/module-setup.sh"        "${pkgdir}/usr/lib/dracut/modules.d/91btrfs-snapshot-overlay/module-setup.sh"
}
