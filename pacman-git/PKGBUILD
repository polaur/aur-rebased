# Maintainer: Piotr Gorski <lucjan.lucjanov@gmail.com>
# Contributor: Dave Reisner <d@falconindy.com>
# Contributor: Thomas Dziedzic < gostrc at gmail >
# Contributor: godane <slaxemulator@gmail.com.com>
# Contributor: Andres Perera <aepd87@gmail.com>

pkgname=pacman-git
pkgver=6.0.1.224.gc8afb0aa
pkgrel=1
pkgdesc="A library-based package manager with dependency support. git version."
arch=('x86_64')
url="http://www.archlinux.org/pacman/"
license=('GPL')
depends=('bash' 'glibc' 'libarchive' 'curl'
         'gpgme' 'pacman-mirrorlist' 'archlinux-keyring')
makedepends=('asciidoc' 'git' 'meson' 'doxygen')
checkdepends=('python' 'fakechroot')
optdepends=('perl-locale-gettext: translation support in makepkg-template')
backup=(etc/pacman.conf
        etc/makepkg.conf)
provides=("pacman=$pkgver" 'libalpm.so')
conflicts=('pacman')
options=('strip' 'debug')
source=(git+https://gitlab.archlinux.org/pacman/pacman
        pacman.conf
        makepkg.conf
        0001-add-support-for-x86_64_v-2-3-4-autodetection.patch
        0001-add-information-about-origin-db.patch
        0001-Add-new-variable-to-declare-package-architecture.patch
        0001-Fix-package-signing.patch
        0001-add-update-message-notice.patch)
sha1sums=('SKIP'
          '018d5d700fcfe7c2ffe60f60a687432283ab5934'
          'becf9c6a0593798c1c3f5b087ac886ae3562e779'
          '161a97f915a7ff8545fcc45c54e371e1b7c3c5a6'
          '31eb4617cb124eb92fb4276a2c4be0c0b8a18fb8'
          'ba72d9b83ce29d8d0ba9ee24ee024b97e5df5750'
          '071c38e7b107fd52f054f5c5fe09749d46886227'
          '7e03c6fe15e5de96d202de25f72e29ee9125c501')

pkgver() {
  cd pacman
  git describe --long | sed 's/^v//;s/-/./g'
}

prepare() {
  cd pacman
  ### Patching sources
  local src
  for src in "${source[@]}"; do
    src="${src%%::*}"
    src="${src##*/}"
    [[ $src = *.patch ]] || continue
    msg2 "Applying patch $src..."
    patch -Np1 < "../$src"
  done
}

build() {
  cd pacman

  meson --prefix=/usr \
        --buildtype=plain \
        -Ddoc=enabled \
        -Ddoxygen=enabled \
        -Dscriptlet-shell=/usr/bin/bash \
        -Dldconfig=/usr/bin/ldconfig \
        build

  meson compile -C build
}

check() {
  cd pacman

  meson test -C build
}

package() {
  cd pacman

  DESTDIR="$pkgdir" meson install -C build

  # install Arch specific stuff
  install -dm755 "$pkgdir/etc"
  install -m644 "$srcdir/pacman.conf" "$pkgdir/etc"
  install -m644 "$srcdir/makepkg.conf" "$pkgdir/etc"
}
