# Maintainer: Piotr Gorski <lucjan.lucjanov@gmail.com>
# Contributor: Haruue Icymoon <haruue@caoyue.com.cn>

pkgbase=linux-usermode-rc
pkgname=('linux-usermode-rc' 'linux-usermode-rc-modules')
_kernelname=-usermodelinux-rc
_major=6.5
_minor=1
_minorc=$((_minor+1))
_rcver=rc1
pkgver=${_major}.${_minorc}
_stable=${_major}.${_minor}
_stablerc=${pkgver}-${_rcver}
_srcname=linux-${_stable}
pkgrel=0
pkgdesc="User mode Linux kernel and modules"
arch=('x86_64')
license=('GPL2')
url="http://user-mode-linux.sourceforge.net/"
depends=('coreutils')
makedepends=('bc' 'inetutils')
options=(!debug)
#_lucjanpath="https://raw.githubusercontent.com/sirlucjan/kernel-patches/master/${_major}"
_lucjanpath="https://gitlab.com/sirlucjan/kernel-patches/raw/master/${_major}"

### Stable-review
# In case the stable-review repository ever stopped working,
# a solution to build rc kernels had to be prepared.

if [ "$_minor" = 0 ]; then
_patchver=${_major}
else
_patchver=${_major}.${_minor}
fi

source=("https://www.kernel.org/pub/linux/kernel/v6.x/linux-${_stable}.tar.xz"
        "https://www.kernel.org/pub/linux/kernel/v6.x/linux-${_stable}.tar.sign"
        "https://www.kernel.org/pub/linux/kernel/v6.x/stable-review/patch-${_stablerc}.xz"
        "https://www.kernel.org/pub/linux/kernel/v6.x/stable-review/patch-${_stablerc}.sign"
        # https://lkml.org/lkml/2019/8/23/712
        # "patch-${_stablerc}.diff::https://git.kernel.org/pub/scm/linux/kernel/git/stable/linux-stable-rc.git/patch/?id=linux-${_major}.y&id2=v${_patchver}"
        'config'
        '70-uml.hook')

sha256sums=('23765dd44425462cd92adbee52670608fd7f3fd183a83b25ba7a7b4883d0451b'
            'SKIP'
            'fd08d136cbefcaf09e16759f2fd940aee65a2f4d396774ea5cfa87e0195750a6'
            'SKIP'
            '3a08fcae0057efff1545432b8a43b11b5213dc9581ae8345f52edaf4ff716f38'
            '05ea4e00d1e99bf8140a21c94e3c42acf17b9debad9c6f5decbe1dd1fe04332c')

validpgpkeys=(
              'ABAF11C65A2970B130ABE3C479BE3E4300411886' # Linus Torvalds
              '647F28654894E3BD457199BE38DBBDC86092693E' # Greg Kroah-Hartman
             )

prepare() {
  cd $_srcname

  echo "Add stable-review patch..."
  patch -p1 -i ../patch-${_stablerc}
  # patch -p1 -i ../patch-${_stablerc}.diff

  echo "Setting version..."
  sed -e "/^EXTRAVERSION =/s/=.*/=/" -i Makefile
  echo "-$pkgrel" > localversion.10-pkgrel
  echo "$_kernelname" > localversion.20-pkgname

  local src
  for src in "${source[@]}"; do
    src="${src%%::*}"
    src="${src##*/}"
    [[ $src = *.patch ]] || continue
    echo "Applying patch $src..."
    patch -Np1 < "../$src"
  done

  echo "Setting config..."
  cp ../config .config
  yes "" | make ARCH=um config >/dev/null
  diff -u ../config .config || :

  make ARCH=um kernelrelease > version
  echo "Prepared $pkgbase version $(<version)"

  echo "Save configuration for later reuse"
  cat .config > "${startdir}/config.last"
}

build() {
  cd ${_srcname}
  unset LDFLAGS CFLAGS
  make ARCH=um vmlinux modules
}

_package() {
  conflicts=('linux-usermode' 'linux-usermode-git' 'linux-usermode-rc-git')

  cd ${_srcname}
  mkdir -p "$pkgdir/usr/bin" "$pkgdir/usr/share/kernel-usermode"
  install -m 644 System.map ${pkgdir}/usr/share/kernel-usermode/System.map
  install -m 755 vmlinux ${pkgdir}/usr/bin/
}

_package-modules() {
  conflicts=('linux-usermode-modules' 'linux-usermode-git-modules'
             'linux-usermode-rc-git-modules')
  depends=('kmod')

  cd ${_srcname}
  local kernver="$(<version)"
  make ARCH=um INSTALL_MOD_PATH="$pkgdir/usr" INSTALL_MOD_STRIP=1 \
    DEPMOD=/doesnt/exist modules_install  # Suppress depmod
  rm -f $pkgdir/usr/lib/modules/${kernver}/{source,build}
  # sed expression for following substitutions
  local _subst="
        s|%PKGBASE%|${pkgbase}|g
        s|%KERNVER%|${kernver}|g
  "
  # install pacman hook
  sed "${_subst}" ../70-uml.hook |
        install -Dm644 /dev/stdin "${pkgdir}/usr/share/libalpm/hooks/70-uml.hook"
}

pkgname=("${pkgbase}" "${pkgbase}-modules")
for _p in ${pkgname[@]}; do
  eval "package_${_p}() {
    $(declare -f "_package${_p#${pkgbase}}")
    _package${_p#${pkgbase}}
  }"
done

# vim:set ts=8 sts=2 sw=2 et:
