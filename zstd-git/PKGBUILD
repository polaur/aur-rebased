# Maintainer: Piotr Górski <lucjan.lucjanov@gmail.com>
# Contributor: Johan Förberg <johan@forberg.se>

pkgname=zstd-git
_pkgname=zstd
pkgver=1.5.3.r9709.g97c23cf61
pkgrel=1
pkgdesc='A fast and efficient compression algorithm.'
arch=('x86_64')
url='https://github.com/facebook/zstd'
license=('BSD' 'GPL2')
depends=('glibc' 'gcc-libs' 'zlib' 'xz' 'lz4')
makedepends=('cmake' 'gtest' 'ninja' 'git')
conflicts=('zstd')
provides=('zstd' 'libzstd.so')
# Use the dev branch by default; upstream merges only full releases to master.
source=('git+https://github.com/facebook/zstd.git#branch=dev')
md5sums=('SKIP')

pkgver() {
  cd $_pkgname
  # git describe
  # git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g;s/^v//g'

  # other
  _ver="$(cat lib/zstd.h | grep -m3 -e ZSTD_VERSION_MAJOR -e ZSTD_VERSION_MINOR -e ZSTD_VERSION_RELEASE | grep -o "[[:digit:]]*" | paste -sd'.')"
  echo "${_ver}.r$(git rev-list --count HEAD).g$(git rev-parse --short HEAD)"
}

prepare() {
  cd $_pkgname
  # avoid error on tests without static libs, we use LD_LIBRARY_PATH
  sed '/build static library to build tests/d' -i build/cmake/CMakeLists.txt
  sed 's/libzstd_static/libzstd_shared/g' -i build/cmake/tests/CMakeLists.txt
}

build() {
  cd $_pkgname
  export CFLAGS+=' -ffat-lto-objects'
  export CXXFLAGS+=' -ffat-lto-objects'

  cmake -S build/cmake -B build -G Ninja \
    -DCMAKE_BUILD_TYPE=None \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DZSTD_BUILD_CONTRIB=ON \
    -DZSTD_BUILD_STATIC=OFF \
    -DZSTD_BUILD_TESTS=ON \
    -DZSTD_PROGRAMS_LINK_SHARED=ON
  cmake --build build
}

#check() {
#  cd $_pkgname
#  export LD_LIBRARY_PATH="$(pwd)/build/lib"
#  ctest -VV --test-dir build
#}

package() {
  cd $_pkgname
  DESTDIR="${pkgdir}" cmake --install build
  ln -sf /usr/bin/zstd "${pkgdir}/usr/bin/zstdmt"
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
