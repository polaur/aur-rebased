# Maintainer: Piotr Górski <lucjan.lucjanov@gmail.com>
# Contributor: Alexander F. Rødseth <xyproto@archlinux.org>
# Contributor: Wesley Moore <wes@wezm.net>

pkgname=bat-polaur-git
_pkgname=bat
pkgver=0.22.1.r7.g2dbc88d3
pkgrel=1
pkgdesc='Cat clone with syntax highlighting and git integration'
arch=('x86_64')
url='https://github.com/sharkdp/bat'
license=('Apache-2.0')
sha256sums=('SKIP')
source=("git+https://github.com/sharkdp/${_pkgname}")
makedepends=('git' 'rust' 'clang')
depends=('libgit2' 'oniguruma')
provides=('bat' 'bat-cat-git')
conflicts=('bat' 'bat-cat-git')

pkgver() {
  cd $_pkgname
  git describe --long --tags | sed 's/v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
  cargo fetch --locked --manifest-path $_pkgname/Cargo.toml
}

build() {
  export CFLAGS+=' -ffat-lto-objects -w'
  cargo build --locked --manifest-path $_pkgname/Cargo.toml --release
}

check() {
  cargo test --locked --manifest-path $_pkgname/Cargo.toml
}

package() {
  install -Dm755 $_pkgname/target/release/$_pkgname "$pkgdir/usr/bin/$_pkgname"

  # Package licenses
  install -Dm644 $_pkgname/LICENSE-APACHE \
    "$pkgdir/usr/share/licenses/$_pkgname/LICENSE-APACHE"
  install -Dm644 $_pkgname/LICENSE-MIT \
    "$pkgdir/usr/share/licenses/$_pkgname/LICENSE-MIT"

  cd $_pkgname/target/release/build

  # Find and package the man page (because cargo --out-dir is too new)
  find . -name bat.1 -type f -exec install -Dm644 {} \
    "$pkgdir/usr/share/man/man1/bat.1" \;

  # Find and package the bash completion file
  find . -name bat.bash -type f -exec install -Dm644 {} \
    "$pkgdir/usr/share/bash-completion/completions/bat" \;

  # Find and package the zsh completion file (not in zsh-completions yet)
  find . -name bat.zsh -type f -exec install -Dm644 {} \
    "$pkgdir/usr/share/zsh/site-functions/_bat" \;

  # Find and package the fish completion file
  find . -name bat.fish -type f -exec install -Dm644 {} \
    "$pkgdir/usr/share/fish/vendor_completions.d/bat.fish" \;
}
