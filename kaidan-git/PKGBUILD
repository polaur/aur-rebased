# Maintainer: Piotr GÃ³rski <lucjan.lucjanov@gmail.com>
# Contributor: Emmanuel Gil Peyrot <linkmauve@linkmauve.fr>

pkgname=kaidan-git
pkgver=0.6.0.9.r1034.gf56e7e4
pkgrel=1
pkgdesc="Simple and user-friendly Jabber/XMPP client for every device"
arch=('x86_64')
url="https://kaidan.im/"
license=('GPL3')
depends=('knotifications' 'kirigami2' 'qt5-location' 'qt5-svg' 'qxmpp' 'zxing-cpp' 'hicolor-icon-theme')
makedepends=('extra-cmake-modules' 'git')
conflicts=('kaidan')
provides=('kaidan')
source=("git://anongit.kde.org/kaidan.git")
sha256sums=('SKIP')

pkgver() {
  cd "$srcdir/kaidan"
  _ver="$(cat CMakeLists.txt | grep -m4 -e VERSION_MAJOR -e VERSION_MINOR -e VERSION_PATCH -e VERSION_CODE | grep -o "[[:digit:]]*" | paste -sd'.')"
  echo "${_ver}.r$(git rev-list --count HEAD).g$(git rev-parse --short HEAD)"
}

prepare() {
  cd "$srcdir/kaidan"
  mkdir -p build
}

build() {
  cd "$srcdir/kaidan/build"
  cmake .. \
    -DI18N=ON
  make
}

package() {
  cd "$srcdir/kaidan/build"
  make DESTDIR="$pkgdir/" install
}
