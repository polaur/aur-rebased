# Maintainer: Piotr GÃ³rski <lucjan.lucjanov@gmail.com>
# Contributor: nontlikeuname

pkgname=meson-git
pkgver=0.60.0.209.gc88bfdbef
pkgrel=1
pkgdesc="SCons-like build system that use python as a front-end language and Ninja as a building backend"
arch=(any)
url="http://mesonbuild.com/"
license=('Apache')
depends=('python-setuptools' 'ninja')
makedepends=('git')
checkdepends=('gcc-objc' 'vala' 'rust' 'gcc-fortran' 'mono' 'boost' 'qt5-base' 'git' 'cython'
              'gtkmm3' 'gtest' 'gmock' 'protobuf' 'wxgtk3' 'python-gobject' 'gobject-introspection'
              'itstool' 'gtk3' 'java-environment=8' 'gtk-doc' 'llvm' 'clang' 'sdl2' 'graphviz'
              'doxygen' 'vulkan-validation-layers' 'openssh' 'mercurial' 'gtk-sharp-2' 'qt5-tools'
              'libwmf' 'valgrind' 'cmake' 'netcdf-fortran' 'openmpi' 'nasm' 'gnustep-base' 'libelf'
              'python-pytest-xdist' 'python2-setuptools') # 'cuda')
provides=('meson')
conflicts=('meson')
source=('git+https://github.com/mesonbuild/meson'
        'arch-meson'
        'skip-test.diff')
md5sums=('SKIP'
         'b85d62ecca0729f0c5168f759a2ff37d'
         'dfd0fec42a3f763283c34a443a6e19c5')

pkgver() {
	cd meson
	
	git describe --tags --long | sed 's/^v//;s/-/./g'
}
         
prepare() {
        cd meson
        
        # Our containers do not allow sanitizers to run
        patch -Np1 -i ../skip-test.diff
}

build() {
        cd meson
        
        python setup.py build
}

# check() {
#	cd meson

#       export LC_CTYPE=en_US.UTF-8 CPPFLAGS= CFLAGS= CXXFLAGS= LDFLAGS=
#       ./run_tests.py
# }

package() {
	cd meson
	
	python setup.py install --root="${pkgdir}" --optimize=1 --skip-build

        install -d "${pkgdir}/usr/share/vim/vimfiles"
        cp -rt "${pkgdir}/usr/share/vim/vimfiles" data/syntax-highlighting/vim/*/

        install -Dt "${pkgdir}/usr/share/bash-completion/completions" -m644 data/shell-completions/bash/*
        install -Dt "${pkgdir}/usr/share/emacs/site-lisp" -m644 data/syntax-highlighting/emacs/*
        install -Dt "${pkgdir}/usr/share/zsh/site-functions" -m644 data/shell-completions/zsh/*

        # Arch packaging helper
        install -D ../arch-meson -t "${pkgdir}/usr/bin"
}
