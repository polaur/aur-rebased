# Maintainer: Piotr GÃ³rski <lucjan.lucjanov@gmail.com>
# Contributor: nontlikeuname

pkgname=meson-git
pkgver=1.4.0.93.g1b9eb6f31
tagver=1.4.0
pkgrel=1
pkgdesc="SCons-like build system that use python as a front-end language and Ninja as a building backend"
arch=(any)
url="http://mesonbuild.com/"
license=('Apache')
depends=('python-setuptools' 'ninja')
pkgdesc="High productivity build system"
url="https://mesonbuild.com/"
arch=(any)
license=(Apache-2.0)
depends=(
  ninja
  python
  python-tqdm
  python-typing_extensions
)
makedepends=(
  python-build
  python-installer
  python-setuptools
  python-wheel
  git
)
checkdepends=(
  boost
  clang
  cmake
  cuda
  cython
  doxygen
  gcc-fortran
  gcc-objc
  git
  glibc-locales
  gmock
  gnustep-base
  gobject-introspection
  graphviz
  gtest
  gtk-doc
  gtk-sharp-2
  gtk3
  gtkmm3
  hotdoc
  itstool
  java-environment=8
  ldc
  libelf
  libwmf
  llvm
  mercurial
  mono
  nasm
  netcdf-fortran
  openmpi
  openssh
  protobuf
  python-gobject
  python-pytest-xdist
  qt5-base
  qt5-tools
  rust
  rust-bindgen
  sdl2
  vala
  valgrind
  vulkan-validation-layers
  wxgtk3
)
provides=('meson')
conflicts=('meson')
source=('git+https://github.com/mesonbuild/meson'
        "meson-reference-$tagver.3::https://github.com/mesonbuild/meson/releases/download/$tagver/meson-reference.3"
        "meson-reference-$tagver.json::https://github.com/mesonbuild/meson/releases/download/$tagver/reference_manual.json"
        'arch-meson'
        'cross-lib32'
        'native-clang'
)
md5sums=('SKIP'
         'b046d3159d3d2df117b40c500eb6c8d3'
         '9810836018bc9b95873e053423459e7c'
         '92cac4ebaa57de9656f48efc688b18b7'
         '8e1d245db195d0d221a5ff083d12f6aa'
         'c941b0388c5d01e26147480a04317871')

pkgver() {
  cd meson
  git describe --tags --long | sed 's/^v//;s/-/./g'
}

build() {
  cd meson
  python -m build --wheel --no-isolation
}

#check() {
#  cd meson
#  export LC_CTYPE=en_US.UTF-8 CPPFLAGS= CFLAGS= CXXFLAGS= LDFLAGS=
#  ./run_tests.py --failfast
#}

package() {
  cd meson
  python -m installer --destdir="$pkgdir" dist/*.whl

  install -d "$pkgdir/usr/share/vim/vimfiles"
  cp -rt "$pkgdir/usr/share/vim/vimfiles" data/syntax-highlighting/vim/*/

  install -Dm644 data/shell-completions/bash/* -t "$pkgdir/usr/share/bash-completion/completions"
  install -Dm644 data/shell-completions/zsh/*  -t "$pkgdir/usr/share/zsh/site-functions"

  install -Dm644 ../meson-reference-$tagver.3    "$pkgdir/usr/share/man/man3/meson-reference.3"
  install -Dm644 ../meson-reference-$tagver.json "$pkgdir/usr/share/doc/$pkgname/reference_manual.json"

  install -D ../arch-meson -t "$pkgdir/usr/bin"

  install -Dm644 ../cross-lib32 "$pkgdir/usr/share/meson/cross/lib32"
  install -Dm644 ../native-clang "$pkgdir/usr/share/meson/native/clang"
}
