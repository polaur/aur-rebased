# Based on linux-firmware by Thomas Bächler <thomas@archlinux.org>
# Maintainer: Piotr Górski <lucjan.lucjanov@gmail.com>
# Contributor: Victor Dmitriyev <mrvvitek@gmail.com>
# Contributor: xduugu

pkgbase=linux-firmware-git
_pkgbase=linux-firmware
pkgname=(linux-firmware-whence-git linux-firmware-git amd-ucode-git
         linux-firmware-{nfp,mellanox,marvell,qcom,liquidio,qlogic,bnx2x}-git
)
pkgver=20230310.r22.gbcdcfbc
pkgrel=1
pkgdesc="Firmware files for Linux"
url="http://git.kernel.org/?p=linux/kernel/git/firmware/linux-firmware.git;a=summary"
license=('GPL2' 'GPL3' 'custom')
arch=('any')
makedepends=('git')
options=(!strip)
source=("git+https://git.kernel.org/pub/scm/linux/kernel/git/firmware/linux-firmware.git"
         '0001-Add-support-for-compressing-firmware-in-copy-firmwar.patch')
md5sums=('SKIP'
         '37b210a7cf7feb7bf0f946b17c16e4d4')

pkgver() {
  cd ${_pkgbase}

  git describe --long --tags | sed 's/v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
  cd ${_pkgbase}

  # add firmware compression support - patch taken from Fedora
  patch -Np1 -i ../0001-Add-support-for-compressing-firmware-in-copy-firmwar.patch
}

build() {
  mkdir -p kernel/x86/microcode
  cat ${_pkgbase}/amd-ucode/microcode_amd*.bin > kernel/x86/microcode/AuthenticAMD.bin

  # Reproducibility: set the timestamp on the bin file
  if [[ -n ${SOURCE_DATE_EPOCH} ]]; then
    touch -d @${SOURCE_DATE_EPOCH} kernel/x86/microcode/AuthenticAMD.bin
  fi

  # Reproducibility: strip the inode and device numbers from the cpio archive
  echo kernel/x86/microcode/AuthenticAMD.bin |
    bsdtar --uid 0 --gid 0 -cnf - -T - |
    bsdtar --null -cf - --format=newc @- > amd-ucode.img
}

_pick() {
  local p="$1" f d; shift
  for f; do
    d="$srcdir/$p/${f#$pkgdir/}"
    mkdir -p "$(dirname "$d")"
    mv "$f" "$d"
    rmdir -p --ignore-fail-on-non-empty "$(dirname "$f")"
  done
}

package_linux-firmware-whence-git() {
  pkgdesc+=" - contains the WHENCE license file which documents the vendor license details"
  conflicts=('linux-firmware-whence')
  provides=("linux-firmware-whence")

  install -Dt "${pkgdir}/usr/share/licenses/${pkgname=}" -m644 ${_pkgbase}/WHENCE
}

package_linux-firmware-git() {
  depends=('linux-firmware-whence-git')
  conflicts=('linux-firmware')
  provides=("linux-firmware")

  cd ${_pkgbase}

  make DESTDIR="${pkgdir}" FIRMWAREDIR=/usr/lib/firmware installcompress

  # Trigger a microcode reload for configurations not using early updates
  echo 'w /sys/devices/system/cpu/microcode/reload - - - - 1' |
    install -Dm644 /dev/stdin "${pkgdir}/usr/lib/tmpfiles.d/${pkgname}.conf"

  install -Dt "${pkgdir}/usr/share/licenses/${pkgname}" -m644 LICEN*

  # split
  cd "${pkgdir}"

  _pick linux-firmware-nfp usr/lib/firmware/netronome
  _pick linux-firmware-nfp usr/share/licenses/${pkgname}/LICENCE.Netronome

  _pick linux-firmware-mellanox usr/lib/firmware/mellanox

  _pick linux-firmware-marvell usr/lib/firmware/{libertas,mwl8k,mwlwifi,mrvl}
  _pick linux-firmware-marvell usr/share/licenses/${pkgname}/LICENCE.{Marvell,NXP}

  _pick linux-firmware-qcom usr/lib/firmware/{qcom,a300_*}
  _pick linux-firmware-qcom usr/share/licenses/${pkgname}/LICENSE.qcom*

  _pick linux-firmware-liquidio usr/lib/firmware/liquidio
  _pick linux-firmware-liquidio usr/share/licenses/${pkgname}/LICENCE.cavium_liquidio

  _pick linux-firmware-qlogic usr/lib/firmware/{qlogic,qed,ql2???_*,c{b,t,t2}fw-*}
  _pick linux-firmware-qlogic usr/share/licenses/${pkgname}/LICENCE.{qla1280,qla2xxx}

  _pick linux-firmware-bnx2x usr/lib/firmware/bnx2x*
}

package_amd-ucode-git() {
  pkgdesc="Microcode update image for AMD CPUs"
  license=('custom')
  provides=("amd-ucode")
  conflicts=("amd-ucode")

  install -Dt "${pkgdir}/boot" -m644 amd-ucode.img

  install -Dt "${pkgdir}/usr/share/licenses/${pkgname}" -m644 ${_pkgbase}/LICENSE.amd-ucode
}

package_linux-firmware-nfp-git() {
  pkgdesc+=" - nfp / Firmware for Netronome Flow Processors"
  depends=('linux-firmware-whence-git')
  provides=("linux-firmware-nfp")
  conflicts=("linux-firmware-nfp")

  mv -v linux-firmware-nfp/* "${pkgdir}"
}

package_linux-firmware-mellanox-git() {
  pkgdesc+=" - mellanox / Firmware for Mellanox Spectrum switches"
  depends=('linux-firmware-whence-git')
  provides=("linux-firmware-mellanox")
  conflicts=("linux-firmware-mellanox")

  mv -v linux-firmware-mellanox/* "${pkgdir}"
}

package_linux-firmware-marvell-git() {
  pkgdesc+=" - marvell / Firmware for Marvell devices"
  depends=('linux-firmware-whence-git')
  provides=("linux-firmware-marvell")
  conflicts=("linux-firmware-marvell")

  mv -v linux-firmware-marvell/* "${pkgdir}"
  # remove arm64 firmware #76583
  rm "${pkgdir}"/usr/lib/firmware/mrvl/prestera/mvsw_prestera_fw_arm64-v4.1.img.xz
}

package_linux-firmware-qcom-git() {
  pkgdesc+=" - qcom / Firmware for Qualcomm SoCs"
  depends=('linux-firmware-whence-git')
  provides=("linux-firmware-qcom")
  conflicts=("linux-firmware-qcom")

  mv -v linux-firmware-qcom/* "${pkgdir}"
}

package_linux-firmware-liquidio-git() {
  pkgdesc+=" - liquidio / Firmware for Cavium LiquidIO server adapters"
  depends=('linux-firmware-whence-git')
  provides=("linux-firmware-liquidio")
  conflicts=("linux-firmware-liquidio")

  mv -v linux-firmware-liquidio/* "${pkgdir}"
}

package_linux-firmware-qlogic-git() {
  pkgdesc+=" - qlogic / Firmware for QLogic devices"
  depends=('linux-firmware-whence-git')
  provides=("linux-firmware-qlogic")
  conflicts=("linux-firmware-qlogic")

  mv -v linux-firmware-qlogic/* "${pkgdir}"
}

package_linux-firmware-bnx2x-git() {
  pkgdesc+=" - bnx2x / Firmware for Broadcom NetXtreme II 10Gb ethernet adapters"
  depends=('linux-firmware-whence-git')
  provides=("linux-firmware-bnx2x")
  conflicts=("linux-firmware-bnx2x")

  mv -v linux-firmware-bnx2x/* "${pkgdir}"
}
