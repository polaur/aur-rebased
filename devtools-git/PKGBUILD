# Maintainer: Piotr GÃ³rski <lucjan.lucjanov@gmail.com>
# Contributor: Pierre Schmitz <pierre@archlinux.de>

pkgname=devtools-git
_pkgname=devtools
pkgver=1.0.3.8.g5479616
pkgrel=1
epoch=1
pkgdesc='Tools for Arch Linux package maintainers'
arch=('any')
license=('GPL')
url='http://projects.archlinux.org/devtools.git/'
depends=( 'arch-install-scripts' 'awk' 'bash' 'binutils'
	  'coreutils' 'diffutils' 'findutils' 'grep' 'fakeroot'
          'jq' 'openssh' 'parallel' 'rsync' 'sed' 'util-linux'
	  'breezy' 'git' 'mercurial' 'subversion')
makedepends=('asciidoc' 'git' 'shellcheck')
optdepends=('btrfs-progs: btrfs support')
provides=("devtools=$pkgver")
conflicts=('devtools')
source=("git+https://github.com/archlinux/devtools.git")
md5sums=('SKIP')

pkgver() {
  cd $_pkgname
  git describe --long --tags | sed 's/^v//;s/-/./g'
}

build() {
  cd $_pkgname
  make BUILDTOOLVER="${epoch}:${pkgver}-${pkgrel}-${arch}" PREFIX=/usr
}

package() {
  cd $_pkgname
  make PREFIX=/usr DESTDIR=${pkgdir} install
}
