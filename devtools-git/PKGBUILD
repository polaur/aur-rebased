# Maintainer: Piotr GÃ³rski <lucjan.lucjanov@gmail.com>
# Contributor: Pierre Schmitz <pierre@archlinux.de>

pkgname=devtools-git
_pkgname=devtools
pkgver=1.3.1.0.g8af7a50
pkgrel=1
epoch=1
pkgdesc='Tools for Arch Linux package maintainers'
arch=('any')
license=('GPL')
url='http://projects.archlinux.org/devtools.git/'
depends=(
  arch-install-scripts
  awk
  bash
  binutils
  coreutils
  curl
  diffutils
  expac
  fakeroot
  findutils
  grep
  jq
  openssh
  parallel
  rsync
  sed
  util-linux

  breezy
  git
  mercurial
  subversion
)
makedepends=(
  asciidoctor
  shellcheck
  git
)
checkdepends=(
  bats
)
optdepends=('btrfs-progs: btrfs support')
provides=("devtools=$pkgver")
conflicts=('devtools')
source=("git+https://github.com/archlinux/devtools.git"
        "0001-makechrootpkg-add-paccache-flag.patch")
md5sums=('SKIP'
         '5f052e1b6db7ea165643a5d0347fecb9')

pkgver() {
  cd $_pkgname
  git describe --long --tags | sed 's/^v//;s/-/./g'
}

prepare() {
  cd $_pkgname
  # we backport way too often in pacman
  # lets at least make it more convenient
  local src
  for src in "${source[@]}"; do
    src="${src%%::*}"
    src="${src##*/}"
    [[ $src = *.patch ]] || continue
    msg2 "Applying patch $src..."
    patch -Np1 < "../$src"
  done
}

build() {
  cd $_pkgname
  make BUILDTOOLVER="${epoch}:${pkgver}-${pkgrel}-${arch}" PREFIX=/usr
}

package() {
  cd $_pkgname
  make PREFIX=/usr DESTDIR=${pkgdir} install
}
