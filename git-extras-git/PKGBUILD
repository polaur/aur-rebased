# Maintainer: Piotr Gorski <lucjan.lucjanov@gmail.com>
# Contributor: Eli Schwartz <eschwartz@archlinux.org>
# Contributor: Joris Steyn <jorissteyn@gmail.com>
# Contributor: Curtis McEnroe <programble@gmail.com>

pkgname=git-extras-git
_pkgname=git-extras
pkgver=6.6.0.dev.r1692.g35100b4
pkgrel=2
pkgdesc="GIT utilities -- repo summary, repl, changelog population, author commit percentages and more"
arch=('any')
url="https://github.com/tj/git-extras"
license=('MIT')
depends=('git')
conflicts=('git-extras')
provides=('git-extras')
source=('git+https://github.com/tj/git-extras'
        '0001-Replace-egrep-with-grep-E.patch')
md5sums=('SKIP'
         'e3e9073818f8586bb3f22eb48e1c773d')

pkgver() {
    cd $_pkgname
    
    # git describe
    # git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
    
    # other
    _ver="$(cat bin/$_pkgname | grep -m1 VERSION | cut -d '"' -f2 | sed 's/-/./g')"
    echo "${_ver}.r$(git rev-list --count HEAD).g$(git rev-parse --short HEAD)"
}

prepare() {
    cd $_pkgname

    local src
    for src in "${source[@]}"; do
      src="${src%%::*}"
      src="${src##*/}"
      [[ $src = *.patch ]] || continue
      echo "Applying patch $src..."
      patch -Np1 < "../$src"
    done
}

package() {
    cd $_pkgname

    # avoid annoying interactive prompts if an alias is in your gitconfig
    export GIT_CONFIG=/dev/null
    make DESTDIR="${pkgdir}" PREFIX=/usr SYSCONFDIR=/etc install
    install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"

    # add documentation for completions, which do not work when autoloaded
    mkdir -p "${pkgdir}"/usr/share/doc/git-extras/
    cp etc/*.zsh etc/*.fish "${pkgdir}"/usr/share/doc/git-extras/
}
