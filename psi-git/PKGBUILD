# Maintainer: Piotr Gorski <lucjan.lucjanov@gmail.com>
# Contributor: Hyacinthe Cartiaux <hyacinthe.cartiaux@free.fr>

pkgname=psi-git
pkgver=1.5.1644.gc6c4413f
pkgrel=1
pkgdesc="A XMPP client with webengine support. GIT version."
arch=('x86_64')
url="https://psi-im.org/"
license=('GPL2')
depends=('qt5-webengine' 'qt5-multimedia' 'qt5-x11extras'
	 'libidn' 'libxss' 'qt5-svg' 'hunspell' 'qtkeychain' 'http-parser')
makedepends=('patch' 'cmake' 'git')
provides=('psi' 'psi-qt5-git' 'psi-git')
conflicts=('psi' 'psi-qt5-git' 'psi-git')
source=('git+https://github.com/psi-im/psi.git'
        'git+https://github.com/psi-im/iris.git'
        'git+https://github.com/psi-im/libpsi.git')
md5sums=('SKIP'
         'SKIP'
         'SKIP')

pkgver() {
  cd psi
  git describe --long --tags | sed 's/^v//;s/-/./g'
}

prepare() {
  cd psi

  # makepkg doesn't support --recursive
  # so setup git modules manually
  git submodule init
  git config submodule.iris.url "$srcdir/iris"
  git config submodule.src/libpsi.url "$srcdir/libpsi"
  # git submodule update
  # From git 2.38.1-1, "git submodule" in PKGBUILD does not work
  # unless we change the git config "protocol.file.allow" [1,2].
  # [1] https://bugs.archlinux.org/task/76255
  # [2] https://bbs.archlinux.org/viewtopic.php?pid=2063104#p2063104
  git -c protocol.file.allow=always submodule update --init --recursive
}

build() {
  cmake -B build -S psi -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_BUILD_TYPE=Release -DBUNDLED_USRSCTP=ENABLE -DBUNDLED_QCA=ENABLE -DPSI_PLUS=OFF -DCHAT_TYPE=WEBENGINE ..
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
