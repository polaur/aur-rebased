# Maintainer: Daniel Karasiński <daniel dot karasinski at gmail dot com>
# Contributor: pavbaranov <pavbaranov at gmail dot com>
# Contributor: nycko
# Contributor: Balló György <ballogyor+arch at gmail dot com>


pkgname=dooble-git
pkgver=2020.10.10.r19.gb362e0bb
pkgrel=1.1
pkgdesc="Web browser based on QtWebEngine"
arch=(x86_64)
url="https://textbrowser.github.io/dooble/"
license=('BSD')
depends=('qt5-webengine')
source=("git+https://github.com/textbrowser/dooble.git"
	dooble_pl_PL.ts)
sha256sums=('SKIP'
            '7105146c25511233836a4884521c402d311ca4b5f23bcabf8be4cccde9037bd0')

pkgver() {
	cd "${pkgname%-*}"
	git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
	cd "${pkgname%-*}/2.x"
  # sed -i 's/-Werror //' dooble.pro
  sed -i 's|Categories=Web|Categories=Network;Qt;WebBrowser;|
          s|Exec=.*|Exec=dooble|
          s|Icon=.*|Icon=dooble|' dooble.desktop
  sed -i 's|QString path(QDir::currentPath());|QString path("/usr/share/dooble");|' Source/dooble_settings.cc
	sed -i 's|QDir::currentPath() + QDir::separator() + "Translations"|"/usr/share/dooble/Translations"|' Source/dooble_application.cc
	sed -i 's|translations.path.*|translations.path = /usr/share/dooble/Translations|' dooble.pro
  cp "${srcdir}"/dooble_pl_PL.ts Translations/
}

build() {
	cd "${pkgname%-*}/2.x"
  qmake dooble.pro
  make
}

package() {
	cd "${pkgname%-*}/2.x"
  install -Dm755 Dooble "$pkgdir/usr/bin/dooble"
  install -Dm644 Icons/Logo/dooble.png "$pkgdir/usr/share/pixmaps/dooble.png"
  install -Dm644 dooble.desktop "$pkgdir/usr/share/applications/dooble.desktop"
  install -Dm644 -t "$pkgdir/usr/share/dooble/Translations" Translations/dooble_*.qm
  install -Dm644 ../LICENSE "$pkgdir/usr/share/licenses/dooble/LICENSE"
}
