# Maintainer: Piotr Gorski <lucjan.lucjanov@gmail.com>
# Contributor: Lukas Fleischer <lfleischer@archlinux.org>
# Contributor: Hilton Medeiros <medeiros.hilton@gmail.com>
# Contributor: Dave Reisner <d@falconindy.com>

pkgname=libgit2-git
_pkgname=libgit2
pkgver=1.5.0.r30.g22f382539
pkgrel=1
epoch=1
pkgdesc='A linkable library for Git'
arch=('x86_64')
url='https://libgit2.github.com/'
depends=('glibc' 'http-parser' 'openssl' 'pcre' 'zlib')
makedepends=('cmake' 'libssh2' 'python' 'git')
provides=('libgit2' 'libgit2.so')
conflicts=('libgit2')
license=('GPL2')
source=("git+https://github.com/libgit2/libgit2"
        'libgit2-1.5.0-remove_http-parse_incompatible_tests.patch')
sha256sums=('SKIP'
            '8e232d3ac6ba756d6055aaccca9d5704417fe77f53ad91898ffbe1b1439523aa')

pkgver() {
  cd "$_pkgname"
  git describe --long --tags | sed 's/v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
  cd "$_pkgname"
  # remove tests that are only compatible with the (modified) vendored version
  # of http-parser, but not with upstream http-parser
  patch -Np1 -i "../libgit2-1.5.0-remove_http-parse_incompatible_tests.patch"
}

build() {
  cd "$_pkgname"
  cmake -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_BUILD_TYPE='None' \
        -DUSE_HTTP_PARSER=system \
        -DTHREADSAFE=ON \
        -Wno-dev \
        -B build \
        -S .
  cmake --build build
}

check() {
  cd "$_pkgname"
  # NOTE: disable tests requiring the internet and relying on non-existent resources
  ctest --test-dir build --output-on-failure -E "invasive|online"
}

package() {
  depends+=('libssh2.so')
  cd "$_pkgname"
  DESTDIR="$pkgdir" cmake --install build
  install -vDm 644 {AUTHORS,README.md} \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
}
