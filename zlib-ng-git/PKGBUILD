# Maintainer: Piotr GÃ³rski <lucjan.lucjanov@gmail.com>
# Contributor: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Chocobo1 <chocobo1 AT archlinux DOT net>
# Contributor: Jacek Szafarkiewicz <szafar at linux dot pl>

pkgbase=zlib-ng-git
_gitname=zlib-ng
pkgname=(zlib-ng-git zlib-ng-compat-git)
pkgver=2.2.1.r8.ge4fb3803
pkgrel=1
pkgdesc='zlib replacement with optimizations for next generation systems'
url='https://github.com/zlib-ng/zlib-ng'
arch=('x86_64')
license=('custom:zlib')
depends=('glibc')
makedepends=('cmake' 'ninja' 'git')
source=("git+https://github.com/zlib-ng/zlib-ng")
sha256sums=('SKIP')
b2sums=('SKIP')

pkgver() {
  cd "$_gitname"

  git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
  cd "$_gitname"

	# WITH_UNALIGNED - unaligned access invokes undefined behaviour,
	#   see https://github.com/gentoo/gentoo/pull/17167 for more info.
  local _options=(
    -G Ninja
    -DCMAKE_BUILD_TYPE=None
    -DCMAKE_INSTALL_PREFIX=/usr
    -DCMAKE_INSTALL_LIBDIR=lib
    -Wno-dev
    -DWITH_GTEST=OFF
    -DWITH_UNALIGNED=OFF
  )

  msg2 "Building zlib-ng"
  cmake -B build \
    "${_options[@]}"
  cmake --build build

  msg2 "Building zlib-ng-compat"
  cmake -B build-compat \
    "${_options[@]}" \
    -DZLIB_COMPAT=ON
  cmake --build build-compat
}

check() {
  cd "$_gitname"
  msg2 "Checking zlib-ng"
  ctest --output-on-failure --test-dir build
  msg2 "Checking zlib-ng-compat"
  ctest --output-on-failure --test-dir build-compat
}

package_zlib-ng-git() {
  provides=('zlib-ng')
  conflicts=('zlib-ng')
  cd "$_gitname"
  DESTDIR="${pkgdir}" cmake --install build
  install -Dm 644 LICENSE.md -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
}

package_zlib-ng-compat-git() {
  pkgdesc+=" (zlib compat)"
  provides=('zlib')
  conflicts=('zlib')

  cd "$_gitname"
  DESTDIR="${pkgdir}" cmake --install build-compat
  install -Dm 644 LICENSE.md -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: ts=2 sw=2 et:
