# Maintainer: Piotr GÃ³rski <lucjan.lucjanov@gmail.com>
# Maintainer: Mark Wagie <mark dot wagie at tutanota dot com>
# Contributor: desbma

pkgname=bat-polaur-extras-git
_pkgname=bat-extras
pkgver=2022.07.27.r9.g38014ba
pkgrel=1
pkgdesc="Bash scripts that integrate bat with various command line tools."
arch=('any')
url="https://github.com/eth-p/bat-extras"
license=('MIT')
depends=('bash' 'bat' 'git' 'man-db' 'ripgrep')
makedepends=('git')
optdepends=('ncurses: optional for batdiff script'
            'git-delta: optional for batdiff script'
            'fzf: optional for batman script'
            'exa: optional for batpipe script'
            'entr: optional for batwatch script'
            'prettier: various code formatting for prettybat script'
            'shfmt: bash formatting for prettybat script'
            'rustfmt: Rust formatting for prettybat script'
            'clang: C / C++ / Objective-C formatting for prettybat script'
            'python-black: Python formatting for prettybat script')
provides=("bat-extras")
conflicts=("bat-extras")
source=('git+https://github.com/eth-p/bat-extras.git'
        'git+https://github.com/eth-p/best.git'
        'git+https://github.com/eth-p/best-tests.git')
sha256sums=('SKIP'
            'SKIP'
            'SKIP')

prepare() {
  cd $_pkgname

  local src
   for src in "${source[@]}"; do
     src="${src%%::*}"
     src="${src##*/}"
     [[ $src = *.patch ]] || continue
     echo "Applying patch $src..."
     patch -Np1 < "../$src"
   done

  git submodule init .test-framework
  git config submodule.best.url "$srcdir/best"
  # From git 2.38.1-1, "git submodule" in PKGBUILD does not work
  # unless we change the git config "protocol.file.allow" [1,2].
  # [1] https://bugs.archlinux.org/task/76255
  # [2] https://bbs.archlinux.org/viewtopic.php?pid=2063104#p2063104
  git -c protocol.file.allow=always submodule update --init --recursive

  cd .test-framework
  git submodule init tests
  git config submodule.best-tests.url "$srcdir/best-tests"
  # From git 2.38.1-1, "git submodule" in PKGBUILD does not work
  # unless we change the git config "protocol.file.allow" [1,2].
  # [1] https://bugs.archlinux.org/task/76255
  # [2] https://bbs.archlinux.org/viewtopic.php?pid=2063104#p2063104
  git -c protocol.file.allow=always submodule update --init --recursive
}

pkgver() {
  cd $_pkgname
  git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

check() {
  cd $_pkgname
  ./test.sh
}

package() {
  cd $_pkgname
  
  ./build.sh \
    --minify=none \
    --no-verify \
    --prefix="$pkgdir/usr" \
    --install

  # documentation
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" \
    CONTRIBUTING.md README.md doc/*
  install -vDm644 -t "$pkgdir/usr/share/man/man1" man/*

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE.md
}
