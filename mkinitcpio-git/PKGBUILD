# Maintainer: Piotr Gorski <lucjan.lucjanov@gmail.com>
# Contributor: Christian Hesse <mail@eworm.de>
# Contributor: Dave Reisner <dreisner@archlinux.org> ([core] package)
# Contributor: Thomas BÃ¤chler <thomas@archlinux.org> ([core] package)

pkgname=mkinitcpio-git
pkgver=28.r0.g3340dbc
pkgrel=1
pkgdesc='Modular initramfs image creation utility - git checkout'
arch=('any')
url='http://www.archlinux.org/'
license=('GPL')
depends=('awk' 'mkinitcpio-busybox>=1.19.4-2' 'kmod' 'util-linux>=2.23' 'libarchive' 'coreutils'
         'bash' 'diffutils' 'findutils' 'grep' 'filesystem>=2011.10-1' 'gzip' 'systemd')
makedepends=('git' 'asciidoc')
optdepends=('xz: Use lzma or xz compression for the initramfs image'
            'bzip2: Use bzip2 compression for the initramfs image'
            'lzop: Use lzo compression for the initramfs image'
            'lz4: Use lz4 compression for the initramfs image'
            'zstd: Use zstd compression for the initramfs image'
            'mkinitcpio-nfs-utils: Support for root filesystem on NFS')
provides=('mkinitcpio' 'initramfs')
conflicts=('mkinitcpio')
backup=('etc/mkinitcpio.conf')
source=('git://projects.archlinux.org/mkinitcpio.git'
        '0001-alpm-hooks-should-use-Type-Path-not-File.patch')
sha256sums=('SKIP'
            '63fd0c170b8b56977a68127213bd62e83d746865f064769c1b8fb813e139541a')

pkgver() {
  cd mkinitcpio
  git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
} 

prepare() {
  cd mkinitcpio
  # Apply 0001-alpm-hooks-should-use-Type-Path-not-File.patch
  patch -Np1 < $srcdir/0001-alpm-hooks-should-use-Type-Path-not-File.patch
}

check() {
  make -C mkinitcpio check
}

package() {
  make -C mkinitcpio DESTDIR="$pkgdir" install
}
