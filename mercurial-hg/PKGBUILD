# Maintainer: Piotr Gorski <lucjan.lucjanov@gmail.com>
# Contributor: Chocobo1 <chocobo1 AT archlinux DOT net>

pkgname=mercurial-hg
pkgver=6.7.2.r53.m55e7784eb3bc
pkgrel=1
pkgdesc="Distributed source control management tool"
arch=('x86_64')
url="https://www.mercurial-scm.org/"
license=(GPL)
depends=('python')
optdepends=('tk: for the hgk GUI')
makedepends=(python-{build,installer,wheel}
             python-setuptools
             python-docutils
             mercurial)
provides=('mercurial')
conflicts=('mercurial')
backup=('etc/mercurial/hgrc')
source=("hg+https://www.mercurial-scm.org/repo/hg#branch=default"
        "mercurial.profile")
sha256sums=('SKIP'
            '87427151713e689cd87dc50d50c048e0e58285815e4eb61962b50583532cbde5')

pkgver() {
  cd "hg"

  hg log -r . -T "{latesttag}{sub('^-0-.*', '', '-{latesttagdistance}-m{node|short}')}" | sed 's/\([^-]*-m\)/r\1/;s/-/./g'
}
            
build() {
  cd "hg"

  python -m build -wn
  make -C contrib/chg
}

package() {
  cd "hg"

  python -m installer -d "$pkgdir" dist/*.whl

  # Do not invoke install target because it invokes a soon to be deprecated
  # `setup.py install` and screws with shebang handling in PEP517 install above
  make DESTDIR="$pkgdir" PREFIX=/usr install-doc

  install -Dm644 contrib/zsh_completion "$pkgdir/usr/share/zsh/site-functions/_hg"
  install -Dm644 contrib/bash_completion "$pkgdir/usr/share/bash-completion/completions/hg"

  make -C contrib/chg DESTDIR="$pkgdir" PREFIX=/usr install

  install -Dm755 contrib/hg-ssh "$pkgdir/usr/bin"
  install -Dm755 contrib/hgk "$pkgdir/usr/bin"

  install -Dm644 -t "$pkgdir/usr/share/emacs/site-lisp/" contrib/{mq.el,mercurial.el}

  install -Dm644 -t "$pkgdir/usr/share/vim/vimfiles/syntax/" contrib/vim/HGAnnotate.vim

  # set some variables
  install -Dm644 "$srcdir/mercurial.profile" "$pkgdir/etc/profile.d/mercurial.sh"

  # FS#38825 - Add certs config to package
  cat <<-EOF | install -Dm755 /dev/stdin "$pkgdir/etc/mercurial/hgrc"
	[web]
	cacerts = /etc/ssl/certs/ca-certificates.crt
	EOF
}
